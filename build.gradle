plugins {
    id "fabric-loom" version "1.7-SNAPSHOT"
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version "1.1.24"
    id 'me.gravityio.replacementplugin' version "0.0.7"
    id "me.gravityio.discord-webhook" version "0.0.2"
}

def changelog = "- Developing for old Minecraft versions should be easier now, the codebase is now based on stonecutter!"
def mcVersions = project.minecraft_supported_versions.replace(" ", "").split(",").toList()
def secrets = new Properties()
def javaVersionNum = project.java_version.toInteger()
def javaVersionObj = JavaVersion.toVersion(javaVersionNum)

rootProject.file('secrets.properties').withInputStream { secrets.load(it) }
rootProject.ext.sharedData = [:]

version = "$rootProject.mod_version+$project.name"
group = "me.gravityio"

println version

if (stonecutter.current.isActive) {
    rootProject.tasks.register("buildActive") {
        group = "project"
        dependsOn(tasks.named("build"))
    }
    rootProject.tasks.register("publishActive") {
        group = "project"
        dependsOn(tasks.named("publishMod"))
    }
}

base {
    archivesName = project.mod_id
}

project.dependencies.add("minecraft", "com.mojang:minecraft:${stonecutter.current.version}")
project.dependencies.add("mappings", loom.officialMojangMappings())
project.dependencies.add("modImplementation", "net.fabricmc:fabric-loader:${rootProject.loader_version}")

repositories {
    // YACL
    maven {
        name "Xander Maven"
        url 'https://maven.isxander.dev/releases'
    }
    // Dependency of YACL
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }

    // Mod Menu
    maven { url "https://maven.terraformersmc.com/releases" }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    if (stonecutter.current.version == "1.20.3" || stonecutter.current.version == "1.20") {
        modImplementation("dev.isxander.yacl:yet-another-config-lib-fabric:$project.yacl_version")
    } else {
        modImplementation("dev.isxander:yet-another-config-lib:$project.yacl_version-fabric")
    }
    modImplementation("com.terraformersmc:modmenu:$project.modmenu_version")
}

loom {
    runConfigs.all {
        ideConfigGenerated(true) // Run configurations are not created for subprojects by default
        runDir = "../../run" // Use a shared run folder and just create separate worlds
    }
}

replacement {
    fileName = "README.md"
    types = ["modrinth", "curseforge"]
    values = [
            "fabric": ["https://modrinth.com/mod/fabric-api", "https://curseforge.com/minecraft/mc-mods/fabric-api"],
            "yacl": ["https://modrinth.com/mod/yacl", "https://curseforge.com/minecraft/mc-mods/yacl"],
            "modmenu": ["https://modrinth.com/mod/modmenu", "https://curseforge.com/minecraft/mc-mods/modmenu"],
            "inventory_tabs": ["https://modrinth.com/mod/inventory-tabs", "https://www.curseforge.com/minecraft/mc-mods/inventory-tabs-updated"]
    ]
}

discordWebhook {
    def modrinthURL = "https://modrinth.com/mod/easyrename/version/${project.version}"
    def curseURL = "https://www.curseforge.com/minecraft/mc-mods/easyrename/files/${rootProject.ext.sharedData.curseFileId}"
    def githubURL = "https://github.com/GravityCY/EasyRename/issues"

    webhook_url = secrets.get("discord_webhook_url") as String
    content = "# ${rootProject.mod_name} ${project.version} Update\n" + changelog + "\n\n[Modrinth](<${modrinthURL}>) | [Curseforge](<${curseURL}>)\n[GitHub](<${githubURL}>)"
}

modrinth {
    token = System.getenv("MODRINTH_UPLOAD")
    projectId = rootProject.modrinth_project_id
    versionName = "Easy Rename $project.version"
    versionNumber = project.version
    uploadFile = remapJar
    gameVersions = mcVersions
    loaders = ["fabric"]
    delegate.changelog = changelog
    dependencies {
        required.project "fabric-api"
        required.project "yacl"
        optional.project "modmenu"
    }
}

tasks.register("publishMod") {
    group = "publishing"

    dependsOn "modrinth"
    dependsOn "publishCurseForge"
    doLast {
        tasks.named("sendDiscordWebhook").execute()
    }
}

tasks.register('publishCurseForge', net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    disableVersionDetection()

    group = "publishing"
    apiToken = System.getenv("CURSEFORGE_UPLOAD");

    def mainFile = upload(rootProject.curseforge_project_id.toInteger(), remapJar)
    mainFile.addEnvironment("Client", "Server")
    mainFile.changelog = changelog
    mcVersions.each {
        mainFile.addGameVersion(it)
    }
    mainFile.displayName = "Easy Rename ${project.version}"
    mainFile.addModLoader("Fabric")
    mainFile.releaseType = "release"
    mainFile.addJavaVersion("Java ${javaVersionNum}")
    mainFile.addRequirement("fabric-api")
    mainFile.addRequirement("yacl")
    mainFile.addOptional("modmenu")

    rootProject.ext.sharedData.curseFileId = mainFile.curseFileId
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

processResources  {
    def modProperties = [
            "mod_id": rootProject.mod_id,
            "mod_name": rootProject.mod_name,
            "mod_version": project.version,
            "mod_description": rootProject.mod_description,
            "mod_author": rootProject.mod_author,
            "mod_sources": rootProject.mod_sources,
            "mod_license": rootProject.mod_license,
            "loader_version": rootProject.loader_version,

            "minecraft_version_range": project.minecraft_version_range,
            "yacl_version": project.yacl_version,
            "fabric_version": project.fabric_version
    ]

    inputs.properties(modProperties)

    filesMatching("fabric.mod.json") {
        expand modProperties
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

