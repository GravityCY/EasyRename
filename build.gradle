plugins {
    id "fabric-loom" version "1.3-SNAPSHOT"
    id "maven-publish"
}

version = project.mod_version
group = "me.gravityio"

project.dependencies.add("minecraft", "com.mojang:minecraft:${project.minecraft_version}")
project.dependencies.add("mappings", "net.fabricmc:yarn:${project.project.yarn_version}:v2")
project.dependencies.add("modImplementation", "net.fabricmc:fabric-loader:${project.loader_version}")

loom {
    accessWidenerPath = file("src/main/resources/easyrename.accesswidener")
}

static def YACL(version) {
    return "dev.isxander.yacl:yet-another-config-lib-fabric:$version"
}

static def GLIB(module, version) {
    return "me.gravityio.goodlib:$module:$version"
}

static def FAPI(module, version) {
    return "net.fabricmc.fabric-api:$module:$version"
}

repositories {
    mavenLocal()
    // Mixin Extras
    maven { url 'https://jitpack.io' }
    // YACL
    maven {
        name "Xander Maven"
        url 'https://maven.isxander.dev/releases'
    }
    // Dependency of YACL
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }

    // Mod Menu
    maven { url "https://maven.terraformersmc.com/releases" }
}

dependencies {
    modImplementation FAPI("fabric-api", project.fabric_version)
    modImplementation YACL(project.yacl_version)
    // My library glib is hosted on mavenLocal for the time being until I learn how all that works
    include modImplementation(GLIB("glib-yacl-utils", "1.0"))

    implementation("com.github.LlamaLad7:MixinExtras:0.1.1")
    annotationProcessor("com.github.LlamaLad7:MixinExtras:0.1.1")
    include("com.github.LlamaLad7:MixinExtras:0.1.1")
}

base {
    archivesName = project.mod_id
}

processResources  {
    def modProperties = [
            "mod_id": project.mod_id,
            "mod_name": project.mod_name,
            "mod_version": project.mod_version,
            "mod_description": project.mod_description,
            "mod_author": project.mod_author,
            "mod_sources": project.mod_sources,
            "mod_license": project.mod_license,
            "loader_version": project.loader_version,
            "minecraft_version": project.minecraft_version
    ]

    inputs.properties(modProperties)

    filesMatching("fabric.mod.json") {
        expand modProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {}
}